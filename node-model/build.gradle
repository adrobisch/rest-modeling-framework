apply plugin: 'antlr'
apply plugin: 'org.xtext.builder'

publishing {
    publications {
        Maven(MavenPublication) {
            from components.java
            artifactId 'node-model'

            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

ext {
    xtextVersion = '2.10.0'
}


xtext {
    version = xtextVersion

    languages {
        ecore {
            setup = 'org.eclipse.xtext.ecore.EcoreSupport'
        }
        codegen {
            setup = 'org.eclipse.emf.codegen.ecore.xtext.GenModelSupport'
        }
        xcore {
            setup = 'org.eclipse.emf.ecore.xcore.XcoreStandaloneSetup'
            generator.outlet.producesJava = true
        }
    }
    sourceSets {
        main {
            srcDir 'model'
            output {
                dir(xtext.languages.xcore.generator.outlet, 'src/main/java-gen')
            }
        }
    }
}

generateGrammarSource {
    outputDirectory = file('src/main/java-gen/io/vrap/rmf/nodes/antlr')
    arguments += ['-visitor']
}

dependencies {
    compile project(':functional-utils')

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.1'

    compile 'org.yaml:snakeyaml:1.18'
    compile 'com.google.guava:guava:23.3-jre'

    compile 'org.eclipse.platform:org.eclipse.equinox.common:3.8.0'
    compile 'org.eclipse.emf:org.eclipse.emf.common:2.11+'
    compile 'org.eclipse.emf:org.eclipse.emf.ecore:2.11+'
    compile 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.100'
    compile "org.eclipse.xtext:org.eclipse.xtext.xbase.lib:$xtextVersion"

    antlr 'org.antlr:antlr4:4.7'

    xtextLanguages 'org.eclipse.jdt:org.eclipse.jdt.core:3.10.0'
    xtextLanguages 'org.eclipse.text:org.eclipse.text:3.5.101'
    xtextLanguages 'org.eclipse.core:org.eclipse.core.resources:3.7.100'
    xtextLanguages "org.eclipse.xtext:org.eclipse.xtext.ecore:$xtextVersion"
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore.xtext:1.2.0'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.common:2.11+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore:2.11+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.11+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore:1.3.1'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.ecore.xcore.lib:1.1.100'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen:2.11+'
    xtextLanguages 'org.eclipse.emf:org.eclipse.emf.codegen.ecore:2.11+'

    testCompile "org.spockframework:spock-core:${versions.spock}"
}
