@Ecore(nsPrefix="resources",nsURI="http://www.vrap.io/raml/resources")
@GenModel(modelDirectory="/raml-model/src/main/java-gen",
    complianceLevel="8.0")
package io.vrap.rmf.raml.model.resources

import io.vrap.rmf.raml.model.facets.ProtocolsFacet

import io.vrap.rmf.raml.model.elements.DocumentableElement

import io.vrap.rmf.raml.model.types.AnnotationsFacet
import io.vrap.rmf.raml.model.types.HeadersFacet
import io.vrap.rmf.raml.model.types.QueryParametersFacet
import io.vrap.rmf.raml.model.types.TypedElement

import io.vrap.rmf.raml.model.security.SecuredByFacet
import io.vrap.rmf.raml.model.elements.IdentifiableElement

import io.vrap.rmf.raml.model.responses.BodyType
import io.vrap.rmf.raml.model.responses.ResponsesFacet

class UriTemplate {
	contains UriTemplatePart[] parts	
}

interface UriTemplatePart {
	
}

class UriTemplateLiteral extends UriTemplatePart {
	String literal	
}

class UriTemplateExpression extends UriTemplatePart {
	String operator
	String[] variables 	
}

class UriParameter extends TypedElement {
}

interface ResourceContainer {
	/*
	 * The (sub-) resources that this element contains. 
	 */
	contains Resource[] resources
}

class Resource extends DocumentableElement, AnnotationsFacet, ResourceContainer, SecuredByFacet {
	/*
	 * The reltaive uri of this resource.
	 */
	contains UriTemplate relativeUri
	
	/*
	 * The uri parameters of this resource.
	 */
	contains UriParameter[] uriParameters
	
	/*
	 * The methods that this resource provides.
	 */
	contains Method[] methods
	
	/*
	 * Returns the method with the given http method or null if
	 * no method with the given http method exists.
	 */
	op Method getMethod(HttpMethod httpMethod) {
		for (Method method : methods) {
			if (method.method == httpMethod) {
				return method
			}
		}
		return null
	}
}

enum HttpMethod {
	^get 
	patch
	put
	post
	delete
	head
	options 
}

class Method extends DocumentableElement, AnnotationsFacet, ProtocolsFacet, SecuredByFacet, 
					 HeadersFacet, QueryParametersFacet, ResponsesFacet {
	/*
	 * The HTTP method.
	 */
	HttpMethod method
	
	/*
	 * The body types of this method.
	 */
	contains BodyType[] bodies
	
	/*
	 * A list of the traits to apply to this method.
	 */
	refers Trait[] is
}

class Trait extends IdentifiableElement, Method {
	String usage
}