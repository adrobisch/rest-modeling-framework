plugins {
    id "de.undercouch.download" version "3.2.0"
}

import de.undercouch.gradle.tasks.download.Download

sourceSets {
    main {
        resources {
            srcDirs += "$buildDir/resources/main/raml"
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'

    compile project(':functional-utils')
    compile project(':raml-model')

    compile 'com.squareup:javapoet:1.9.0'
    compile 'com.google.guava:guava:22.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.9'
}

task downloadCtpApiZipFile(type: Download) {
    src 'https://github.com/commercetools/commercetools-api-reference/archive/master.zip'
    dest new File(buildDir, 'commercetools-api-reference.zip')
    onlyIfNewer(true)
    overwrite(false)
}

task downloadAndUnzipCtpApiFile(dependsOn: downloadCtpApiZipFile, type: Copy) {
    from zipTree(downloadCtpApiZipFile.dest)
    into "$buildDir/resources/main/raml"
}

build.dependsOn(downloadAndUnzipCtpApiFile)
